model
{
    mu.psi ~ dnorm(0.00000E+00, 0.01)
    mu.p ~ dnorm(0.00000E+00, 0.01)

    sigma.psi.sp ~ dunif(0.00000E+00, 10)
    sigma.p.sp ~ dunif(0.00000E+00, 10)
    tau.psi.sp <- 1/(sigma.psi.sp * sigma.psi.sp)
    tau.p.sp <- 1/(sigma.p.sp * sigma.p.sp)
    for (sp in 1:nsp) {
        psi.sp[sp] ~ dnorm(0.00000E+00, tau.psi.sp)
        p.sp[sp] ~ dnorm(0.00000E+00, tau.p.sp)
    }

    sigma.p.site ~ dunif(0.00000E+00, 10)
    tau.p.site <- 1/(sigma.p.site * sigma.p.site)
    for (site in 1:nsite) {
        for (era in 1:nera) {
            p.site[site, era] ~ dnorm(0.00000E+00, tau.p.site)
        }
    }
    for (era in 2:nera){
    	mu.psi.era[era] ~ dnorm(0.00000E+00, 0.01)
    	sigma.psi.era[era] ~ dunif(0.00000E+00, 10)
    	tau.psi.era[era] <- 1/(sigma.psi.era[era] * sigma.psi.era[era])
	}
    for (sp in 1:nsp) {
        psi.era[sp,1] <- 0
        for (era in 2:nera) {
        	psi.era[sp,era] ~ dnorm(mu.psi.era[era], tau.psi.era[era])
	}
    }
    
    p.era[1] <- 0
    for (era in 2:nera) {
    	p.era[era] ~ dnorm(0.00000E+00, 0.01)
    }
    
    for (sp in 1:nsp) {
        for (era in 1:nera) {
            logit(psi[sp,era]) <- mu.psi + psi.sp[sp] + psi.era[sp,era]
            for (site in 1:nsite) {
                logit(p[sp,site,era]) <- mu.p + p.sp[sp] + p.site[site,era] + p.era[era]
            }
        }
    }

    for (sp in 1:nsp) {
        for (site in 1:nsite) {
            for (era in 1:nera) {
                Z[sp,site,era] ~ dbern(psi[sp,era])
            }
        }
    }

    for (ind in 1:nind) {
        p.eff[ind] <- Z[sp[ind],site[ind],era[ind]] * p[sp[ind],site[ind],era[ind]]
        X[ind] ~ dbern(p.eff[ind])
    }
}
